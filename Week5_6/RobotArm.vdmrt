class RobotArm

types
    public Angle = real inv N == N >= 0 and N <= 1.6; -- begrænsning N ca 0 til 90 grader

values 
    public N1 : real = 0;
    public N2 : real =1.6;

    r: real = 0.8; -- arm lægnde i m
    mass: real = 1.2; -- kg
    Fc: real = 0.1; -- Coulomb friktionskoefficient
    Fv: real = 0.05; -- viskøs friktionskoefficient


instance variables

    public env_q : real; -- kg, angle of roint. radianer
    public env_qt : real;
    
    public env_q_vel: real; -- hastighed. rad/s
    public env_vel_change: real; --
    public env_q_acc: real; -- acceleration. rad/s^2
    
    --! PID controller

    public kp: real; -- proportional factor
    public kd: real; -- derivative factor
    public prevError: real; -- radianer

    public accumulatedTime: real; -- simuleret tid
    public env_motorOn: bool := false; -- 1=running, 0=stopped

operations
    public RobotArm: () ==> RobotArm
    RobotArm() == (
        env_motorOn := false; -- starter som slukket
        env_q := 0;
        env_qt := 0;

        env_q_vel := 0; 
        kp := 0.1; 
        kd := 0.01;
        env_q_acc := 0; 
        prevError := 0; 
        accumulatedTime := 0; 
        env_vel_change := 0;
    );

    --! Making sure connection between controller and robotarm is up. 
    public IsReady: () ==> bool
        IsReady() == (
        return true; 
    );

    public TurnMotorOn: () ==> ()
    TurnMotorOn() == (
        env_motorOn := true;
    );

    public TurnMotorOff: () ==> ()
    TurnMotorOff() == (
        env_motorOn := false;
    );

    public SetTargetPosition: Angle ==> ()
    SetTargetPosition(target) == (
        env_qt := target;
    );

    public SetCurrentPosition: Angle ==> ()
    SetCurrentPosition(position) == (
        env_q := position;
    );

    public SetKp: real ==> ()
    SetKp(k_p) == (
        kp := k_p;
    );

    public SetKd: real ==> ()
    SetKd(k_d) == (
        kd := k_d;
    );
    
    public Step: real ==> ()
    Step(delta) == duration(0) (
        if env_motorOn then (
        dcl errorPos : real := env_qt - env_q,
            errorDerivative : real := (errorPos - prevError) / delta,
            motorTorque : real := (kp * errorPos) + (kd * errorDerivative);
            dcl I : real := 2 * r * r; -- antager en standard inertimoment 2*r^2
        
        -- Update previous error
        prevError := errorPos;

        env_vel_change := env_q_vel;

        -- Update momvement values
        env_q_acc := motorTorque / I; -- antager uniform massefordeling armlængde meter. a = t / i
        env_q_vel := env_q_vel + env_q_acc * delta;
        env_q := env_q + env_q_vel * delta;

        -- Update time
        accumulatedTime := accumulatedTime + delta;

        --! Boundary conditions
        if env_q < 0 then env_q := 0
        elseif env_q > 1.6 then env_q := 1.6;
        ) else (
            env_q_acc := 0;
            env_q_vel := 0;
        );

    );
    
end RobotArm